buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "com.android.tools.build:gradle:3.3.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

group = "com.clarabridge"

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.github.dcendents.android-maven"

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

version = "$versionMajor.$versionMinor.$versionPatch"

android {
    compileSdkVersion 29
    defaultPublishConfig "release"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            consumerProguardFiles "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        test.java.srcDirs += "src/test/kotlin"
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging "aar"
                name "ClarabridgeChat"
                url "http://clarabridgechat.io"
            }
        }
    }
}

def glideVersion = "4.9.0" // Limited by androidx
def playServicesVersion = "16.0.0" // Limited by androidx
def subsamplingScaleImageViewVersion = "3.10.0"

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation "com.google.android.gms:play-services-location:${playServicesVersion}"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view:${subsamplingScaleImageViewVersion}"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-inline:3.3.0"
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    api project(":core")
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    source += fileTree(dir: "../core/src/main/java/") + fileTree(dir: "../ui/src/main/java/")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    include "com/clarabridge/core/*"
    include "com/clarabridge/ui/*"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    baseName = "clarabridgeChat"
    from javadoc.destinationDir
}


def classesFilename = "ui-$version-classes.jar"

task copyClassesJar(type: Copy) {
    from("build/intermediates/intermediate-jars/release/")
    into("build/libs/")
    include("classes.jar")
    rename("classes.jar", classesFilename)
}

task cleanClassesJar(type: Delete) {
    delete "build/libs/$classesFilename"
}

install.dependsOn copyClassesJar
clean.dependsOn cleanClassesJar

artifacts {
    def classesJarFile = file("build/libs/$classesFilename")
    archives classesJarFile
}
