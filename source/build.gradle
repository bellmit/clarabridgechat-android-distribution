// Top-level build file where you can add configuration options common to all sub-projects/modules.
if (file("$rootDir/analysis/analysis.gradle").exists()) {
    apply from: "$rootDir/analysis/analysis.gradle"
}

buildscript {
    ext {
        versions = [
                kotlin: "1.3.60"
        ]
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://dl.bintray.com/android/android-tools" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.0"
        classpath "com.google.gms:google-services:4.3.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    apply plugin: "maven-publish"

    repositories {
        google()
        maven { url "https://maven.google.com" }
        mavenCentral()
    }
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                version version
                artifactId project.name
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            }
        }
        repositories {
            maven {
                url s3Bucket
                name repoName
                credentials(AwsCredentials) {
                    accessKey s3AccessKey
                    secretKey s3SecretKey
                    sessionToken s3SessionToken
                }
            }
        }
    }
}
